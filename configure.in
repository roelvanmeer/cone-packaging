dnl Process this file with autoconf to produce a configure script.
# $Id: configure.in,v 1.79 2007/09/29 21:36:19 mrsam Exp $
#
# Copyright 2003-2007, Double Precision Inc.
#
# See COPYING for distribution information.
#
AC_PREREQ(2.59)
AC_INIT(cone,0.72,[courier-cone@lists.sourceforge.net])
>confdefs.h  # Kill PACKAGE_ macros
AC_CONFIG_SRCDIR(cone.spec.in)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE

ALL_LINGUAS=
LPATH="$PATH:/usr/local/bin"

test "x$prefix" = xNONE && prefix=$ac_default_prefix
test "x$exec_prefix" = xNONE && exec_prefix='${prefix}'

eval "exec_prefix=$exec_prefix"
eval "bindir=$bindir"
eval "sysconfdir=$sysconfdir"
eval "datadir=$datadir"

dnl Checks for programs.
AC_PROG_AWK
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_SYSCONFTOOL
AM_PROG_LIBTOOL
AC_PATH_PROGS(PERL, perl5 perl, perl, $LPATH)
dnl Checks for libraries.
dnl Replace `main' with a function in -lefence:

AC_ARG_ENABLE(unicode, [], [],
		       ac_configure_args="$ac_configure_args --enable-unicode")

AM_CONDITIONAL(HAVE_SGML, test -d "$srcdir/docbook")

AC_PATH_PROGS(COURIERCONFIG, courier-config, none, $LPATH)

AC_ARG_WITH(certdb, [  --without-certdb        Do not install default root certificates
  --with-certdb=/pathname     Use external root certificate database in pathname], [

  if test "$withval" = "yes"
  then
changequote(<<,>>)

	ac_configure_args=`echo "$ac_configure_args" | sed "s@--without-certdb@--with-certdb@;s@--with-certdb=[^ ']*@--with-certdb@;s@--with-certdb@--with-certdb="$datadir/cone/rootcerts'@'`

changequote([,])

  fi

  ], [

  if test "$COURIERCONFIG" != "none"
  then
	$COURIERCONFIG >conftemp || exit 1
	rootcerts=`. conftemp ; echo $datadir/rootcerts`
	rm -f conftemp

	if test -d "$rootcerts/."
	then
		ac_configure_args="$ac_configure_args --without-certdb --with-courierrootcert=$rootcerts"
	else
		ac_configure_args="$ac_configure_args --with-certdb=$datadir/cone/rootcerts"
	fi
  else
	ac_configure_args="--with-certdb=$datadir/cone/rootcerts $ac_configure_args"
  fi
])

echo "$VERSION" >coneversion
AC_CONFIG_SUBDIRS(unicode numlib liblock soxwrap md5 sha1 libhmac random128 rfc1035 rfc822 rfc2045 gpglib maildir tcpd curses ldapaddressbook libmail rootcerts cone)
AC_OUTPUT(Makefile cone.spec)
